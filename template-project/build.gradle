import org.apache.ivy.plugins.resolver.URLResolver

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'
apply plugin: 'idea'

def compatibilityVersion = 1.5
sourceCompatibility = compatibilityVersion
targetCompatibility = compatibilityVersion

repositories {
    mavenCentral()
}

sourceSets {
    main {
        output.classesDir file('war/WEB-INF/classes')
    }
}

dependencies {
    groovy 'org.codehaus.groovy:groovy-all:1.8.4'
    testCompile 'javax.servlet:servlet-api:2.5'
}

webAppDirName = file('war')

clean {
     delete sourceSets*.output.classesDir
}

task copyRuntimeLibraries(type: Sync){
    def webAppLibDirName = 'war/WEB-INF/lib'
    description = "Copies runtime libraries to $webAppLibDirName."
    from configurations.runtime  
    into webAppLibDirName
}

idea {
    project {
        jdkName = compatibilityVersion

        ipr.withXml { provider ->
            def node = provider.asNode()

            // Set Gradle home
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
            gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir])
        }
    }
}

eclipse {
    project {
        name 'caelyf-project'
        file {
            whenMerged { project ->
                project.natures << 'com.google.gdt.eclipse.core.webAppNature'
            }
        }
    }
    classpath {
        file {
            withXml { xml ->
                xml.asNode().classpathentry.find { it.@kind == 'output' && it.@path == 'bin' }.@path = 'war/WEB-INF/classes'
                xml.asNode().appendNode('attribute', [name: 'org.eclipse.jst.component.nondependency', value: '/war/WEB-INF/lib'])
                xml.asNode().appendNode('classpathentry', [exported: 'true', kind: 'con', path: 'GROOVY_SUPPORT'])
                            .appendNode('attributes')
                            .appendNode('attribute', [name: 'org.eclipse.jst.component.nondependency', value: '/war/WEB-INF/lib'])
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion '1.0-milestone-6'
}